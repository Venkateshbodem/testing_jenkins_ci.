plugins {
    id 'java'
    id 'maven-publish'
}

group = 'org.gradle.sample'
version = '1.0'

tasks.register('sourcesJar', Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

// tag::publishing[]
publishing {
    publications {
        binary(MavenPublication) {
            from components.java
        }
        binaryAndSources(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
    repositories {
        // change URLs to point to your repos, e.g. http://my.org/repo
        maven {
            name = 'external'
            url = layout.buildDirectory.dir('repos/external')
        }
        maven {
            name = 'internal'
            url = layout.buildDirectory.dir('repos/internal')
        }
    }
}
// end::publishing[]

// tag::task-config[]
tasks.withType(PublishToMavenRepository) {
    def predicate = repository.zip(publication) { repo, pub ->
        (repo == publishing.repositories.external && pub == publishing.publications.binary)
            || (repo == publishing.repositories.internal && pub == publishing.publications.binaryAndSources)
    }
    onlyIf("publishing binary to the external repository, or binary and sources to the internal one") {
        predicate.get()
    }
}
tasks.withType(PublishToMavenLocal) {
    def predicate = publication.map { pub ->
        pub == publishing.publications.binaryAndSources
    }
    onlyIf("publishing binary and sources") {
        predicate.get()
    }
}
// end::task-config[]

// tag::shorthand-tasks[]
tasks.register('publishToExternalRepository') {
    group = 'publishing'
    description = 'Publishes all Maven publications to the external Maven repository.'
    dependsOn tasks.withType(PublishToMavenRepository).matching {
        it.repository.get() == publishing.repositories.external
    }
}
// end::shorthand-tasks[]
tasks.register('publishForDevelopment') {
    group = 'publishing'
    description = 'Publishes all Maven publications to the internal Maven repository and the local Maven repository.'
    dependsOn tasks.withType(PublishToMavenRepository).matching {
        it.repository.get() == publishing.repositories.internal
    }
    dependsOn tasks.withType(PublishToMavenLocal)
}
