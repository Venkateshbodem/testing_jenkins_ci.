/*
 * Copyright 2010 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'java-library'
}

sourceCompatibility = javaVersion.java9Compatible ? 1.6 : 1.5

configurations {
    reports
}

classpathManifest {
    optionalProjects = ['gradle-kotlin-dsl']
}

dependencies {
    api project(":baseServices")
    api project(":baseServicesGroovy")
    api project(":messaging")
    api project(":logging")
    api project(":resources")
    api project(":cli")
    api project(":buildOption")
    api project(":native")
    api project(":persistentCache")
    api project(":buildCache")
    api project(":coreApi")

    api project(":processServices")
    api project(":jvmServices")
    api project(":modelCore")
    api Libraries.groovy.coordinates
    api Libraries.ant.coordinates
    api Libraries.guava.coordinates
    api Libraries.inject.coordinates

    implementation project(":modelGroovy")
    implementation Libraries.asm.coordinates
    implementation Libraries.asmCommons.coordinates
    implementation Libraries.slf4jApi.coordinates
    implementation Libraries.commonsCollections.coordinates
    implementation Libraries.commonsIo.coordinates
    implementation Libraries.commonsLang.coordinates
    implementation Libraries.jcip.coordinates
    implementation Libraries.nativePlatform.coordinates
    implementation Libraries.commonsCompress.coordinates
    implementation Libraries.xmlApis.coordinates

    runtimeOnly project(":docs")

    testImplementation TestLibraries.jsoup
    testImplementation Libraries.log4jToSlf4j.coordinates

    testRuntimeOnly Libraries.xerces.coordinates
    testRuntimeOnly project(":diagnostics")
    testRuntimeOnly project(":compositeBuilds")

    testFixturesImplementation project(":internalTesting")
    testFixturesImplementation Libraries.ivy.coordinates

    testFixturesRuntime project(':runtimeApiInfo')
    testFixturesRuntime rootProject.ext.allCoreRuntimeExtensions
    testFixturesRuntime project(':testingJunitPlatform')

    integTestImplementation project(":internalIntegTesting")

    integTestRuntimeOnly project(":plugins")
    integTestRuntimeOnly project(':maven')
}

testFixtures {
    from(':core')
    from(':coreApi')
    from(':messaging')
    from(':modelCore')
    from(':logging')
    from(':baseServices')
}

test {
    forkEvery = 200
}

[compileGroovy, compileTestGroovy]*.groovyOptions*.fork(memoryInitialSize: '128M', memoryMaximumSize: '1G')

task pluginsManifest(type: WriteProperties) {
    property("plugins") {
        project.pluginProjects.collect { it.archivesBaseName }.sort().join(',')
    }
    outputFile = new File(project.generatedResourcesDir, "gradle-plugins.properties")
}

sourceSets.main.output.dir generatedResourcesDir, builtBy: pluginsManifest

task implementationPluginsManifest(type: WriteProperties) {
    property("plugins") {
        project.implementationPluginProjects.collect { it.archivesBaseName }.sort().join(',')
    }
    outputFile = new File(project.generatedResourcesDir, "gradle-implementation-plugins.properties")
}

sourceSets.main.output.dir generatedResourcesDir, builtBy: implementationPluginsManifest

verifyTestFilesCleanup.errorWhenNotEmpty = false
