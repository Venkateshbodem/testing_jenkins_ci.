/*
 * Copyright 2023 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package org.gradle.api.internal.cache

import org.gradle.integtests.fixtures.AbstractIntegrationSpec

class CacheMarkingIntegrationTest extends AbstractIntegrationSpec {
    @Override
    def setup() {
        executer.requireOwnGradleUserHomeDir()
    }

    def writeInitScript(boolean mark) {
        def initDir = new File(executer.gradleUserHomeDir, "init.d")
        initDir.mkdirs()
        new File(initDir, "cache-settings.gradle") << """
            beforeSettings { settings ->
                settings.caches {
                    mark = $mark
                }
            }
        """
    }

    def "directory '#directory' is #expectedState when mark = #mark and directory created = #createDir"() {
        writeInitScript(mark)
        def dir = new File(executer.gradleUserHomeDir, directory)
        if (createDir) {
            dir.mkdirs()
        }

        when:
        succeeds("help")

        then:
        new File(dir, "CACHEDIR.TAG").exists() == (expectedState == "marked")

        where:
        directory       | mark  | createDir | expectedState
        // Wrapper Dists not used by test framework, so not marked if not created
        "wrapper/dists" | false | false     | "not marked"
        "wrapper/dists" | false | true      | "not marked"
        "wrapper/dists" | true  | false     | "not marked"
        "wrapper/dists" | true  | true      | "marked"
        // JDKs are not used by this test, so not marked if not created
        "jdks"          | false | false     | "not marked"
        "jdks"          | false | true      | "not marked"
        "jdks"          | true  | false     | "not marked"
        "jdks"          | true  | true      | "marked"
        // Caches will be generated by test framework, so marked even if not created
        "caches"        | false | false     | "not marked"
        "caches"        | false | true      | "not marked"
        "caches"        | true  | false     | "marked"
        "caches"        | true  | true      | "marked"
        // Daemon logs will be generated by test framework sometimes, so marked even if not created
        "daemon"        | false | false     | "not marked"
        "daemon"        | false | true      | "not marked"
        "daemon"        | true  | false     | (executer.isUseDaemon() ? "marked" : "not marked")
        "daemon"        | true  | true      | "marked"
    }

    def "only specific directories are marked by Gradle as caches"() {
        def dirsThatShouldBeMarked = [
            "caches",
            "daemon",
            "jdks",
            "wrapper/dists",
        ]
        def dirsThatShouldNotBeMarked = [
            "build-scan-data",
            "enterprise",
            "init.d",
            "native",
            "nodejs",
            "notifications",
            "undefined-build",
            "workers",
            "wrapper",
            "yarn",
        ]
        writeInitScript(true)
        for (dir in (dirsThatShouldBeMarked + dirsThatShouldNotBeMarked)) {
            new File(executer.gradleUserHomeDir, dir).mkdirs()
        }

        when:
        succeeds("help")

        then:
        for (dir in dirsThatShouldBeMarked) {
            assert new File(new File(executer.gradleUserHomeDir, dir), "CACHEDIR.TAG").exists()
        }
        for (dir in dirsThatShouldNotBeMarked) {
            assert !new File(new File(executer.gradleUserHomeDir, dir), "CACHEDIR.TAG").exists()
        }
    }
}
