apply from: "$rootDir/gradle/classycle.gradle"

dependencies {
    groovy libraries.groovy

    integTestCompile project(':toolingApi')
    integTestCompile project(':launcher')
    integTestCompile project(':coreImpl')
    integTestCompile libraries.ant
    integTestCompile libraries.xmlunit
    integTestCompile libraries.nekohtml

    integTestRuntime rootProject.configurations.testRuntime.allDependencies
}

integTestTasks.all {
    dependsOn({ rootProject.getTasksByName('publishLocalArchives', true) }, ':docs:userguideDocbook')

    jvmArgs '-Xmx512m', '-XX:MaxPermSize=256m'

    doFirst {
        systemProperties['integTest.userGuideInfoDir'] = project(':docs').docbookSrc
        systemProperties['integTest.userGuideOutputDir'] = new File(project(':docs').samplesSrcDir, "userguideOutput").absolutePath
        forkEvery = 15
    }

    // You can filter the userguide samples to be run by specifying this system property.
    // E.g. ./gradlew integTest:integTest -D:integTest:integTest.single=UserGuideSamplesIntegrationTest -Dorg.gradle.userguide.samples.filter=signing/.+
    systemProperty "org.gradle.userguide.samples.filter", System.getProperty("org.gradle.userguide.samples.filter")
}

daemonIntegTest {
    exclude "**/CrossVersionCompatibilityIntegrationTest.class" //ignored just in case to avoid old daemon implementation
    exclude "**/DistributionIntegrationTest.class" //fragile - heavily depends on external repos and does not contribute too much to the daemon suite anyway
}

eclipse {
	classpath {
		file.whenMerged { classpath ->
            classpath.entries.removeAll { it.path.contains('src/integTest/resources') }
		}
	}
}
