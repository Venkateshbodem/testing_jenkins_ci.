/*
 * Copyright 2014 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package org.gradle.play.plugins

import org.gradle.integtests.fixtures.AbstractIntegrationSpec
import org.gradle.integtests.fixtures.TestResources
import org.gradle.test.fixtures.archive.JarTestFixture
import org.gradle.util.AvailablePortFinder
import org.gradle.util.TextUtil
import org.junit.Rule
import spock.lang.Timeout

class PlayApplicationPluginIntegrationTest extends AbstractIntegrationSpec {

    @Rule
    public final TestResources resources = new TestResources(temporaryFolder)

    def setup() {
        settingsFile << """
        rootProject.name = 'play-app'
"""
        buildFile << """
        plugins {
            id 'play-application'
        }

        model {
            components {
                myApp(PlayApplicationSpec)
            }
        }

        tasks.withType(ScalaCompile) {
            scalaCompileOptions.forkOptions.memoryMaximumSize = '1g'
            scalaCompileOptions.forkOptions.jvmArgs = ['-XX:MaxPermSize=512m']
        }

        tasks.withType(TwirlCompile){
            forkOptions.memoryInitialSize =  "256m"
            forkOptions.memoryMaximumSize =  "512m"
        }
"""
    }

    def "can register PlayApplicationSpec component"() {
        when:
        succeeds "components"

        then:
        output.contains(TextUtil.toPlatformLineSeparators("""
DefaultPlayApplicationSpec 'myApp'
----------------------------------

Source sets
    No source sets.

Binaries
    DefaultPlayApplicationBinarySpec 'myAppBinary'
        build using task: :myAppBinary
        platform: PlayPlatform2.3.5
        tool chain: Default Play Toolchain"""))
    }

    def "cannot register multiple PlayApplicationSpec components"() {
        given:
        buildFile << """
        model {
             components {
                 myOtherApp(PlayApplicationSpec)
             }
        }
"""
        when:
        fails "components"

        then:
        failure.assertHasDescription("A problem occurred configuring root project 'play-app'.")
        failure.assertHasCause("Multiple components of type 'PlayApplicationSpec' are not supported.")
    }

    def "builds empty play binary when no sources present"() {
        when:
        succeeds("assemble")

        then:
        executedAndNotSkipped(":createMyAppBinaryJar", ":myAppBinary", ":assemble")
        skipped(":routesCompileMyAppBinary" , ":twirlCompileMyAppBinary", ":scalaCompileMyAppBinary")

        and:
        jar("build/jars/myApp/myAppBinary.jar").hasDescendants()
    }

    def "can build play apps generated by 'play new'"() {
        given:
        resources.maybeCopy("PlayApplicationPluginIntegrationTest/playNew")
        buildFile << """
        repositories{
            jcenter()
            maven{
                name = "typesafe-maven-release"
                url = "http://repo.typesafe.com/typesafe/maven-releases"
            }
        }
"""
        when:
        succeeds("assemble")
        then:
        executedAndNotSkipped(":routesCompileMyAppBinary", ":twirlCompileMyAppBinary", ":createMyAppBinaryJar", ":myAppBinary", ":assemble")

        and:
        jar("build/jars/myApp/myAppBinary.jar").containsDescendants(
                "Routes.class",
                "views/html/index.class",
                "views/html/main.class",
                "controllers/Application.class",
                "images/favicon.png",
                "stylesheets/main.css",
                "javascripts/hello.js",
                "application.conf")

        when:
        succeeds("createMyAppBinaryJar")
        then:
        skipped(":createMyAppBinaryJar", ":twirlCompileMyAppBinary")
    }

    /**
     * This tests the right behaviour of the "run" task provided by the play plugin.
     * For further testing of correct app behaviour we should setup a more manageable
     * test fixture based on play.core.server.ServerProcess
     * */
    @Timeout(60)
    def "can run play app generated by 'play new'"() {
        setup:
        def httpPort = AvailablePortFinder.createPrivate().nextAvailable
        when:
        resources.maybeCopy("PlayApplicationPluginIntegrationTest/playNew")
        buildFile << """
        repositories{
            jcenter()
            maven{
                name = "typesafe-maven-release"
                url = "http://repo.typesafe.com/typesafe/maven-releases"
            }
        }

        model {
            tasks.runMyAppBinary {
                ignoreExitValue = true
                String content;
                systemProperty("http.port", "$httpPort")
                doFirst{
                    Thread.start{
                        available("http://localhost:$httpPort")
                        content = new URL("http://localhost:$httpPort").text
                        String pid = file('RUNNING_PID').text
                        project.exec{
                            if(org.gradle.internal.os.OperatingSystem.current().isWindows()){
                                executable "taskkill"
                                args "/F", "/PID", pid
                            }else{
                                executable "kill"
                                args "-9", pid
                            }
                            ignoreExitValue = true
                        }
                    }
                }
                doLast{
                    file('RUNNING_PID').delete()
                    assert content.contains("Your new application is ready.")
                }
            }
        }

        def available(String theUrl, int timeout = 30000) {
            URL url = new URL(theUrl)
            long expiry = System.currentTimeMillis() + timeout
            while (System.currentTimeMillis() <= expiry) {
                try {
                    url.text
                    return
                } catch (IOException e) {
                    // continue
                }
                Thread.sleep(200)
            }
            throw new RuntimeException("Timeout waiting for \$theUrl to become available.");
        }
"""

        then:
        succeeds("runMyAppBinary")
    }

    JarTestFixture jar(String fileName) {
        new JarTestFixture(file(fileName))
    }
}