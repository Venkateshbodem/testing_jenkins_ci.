apply plugin: "groovy"

// Compiler daemon
sourceCompatibility = 1.6

sourceSets {
    integTestPrepare {
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
}

configurations {
    integTestPrepareCompile.extendsFrom integTestCompile
    integTestPrepareImplementation.extendsFrom integTestImplementation
    integTestPrepareRuntime.extendsFrom integTestRuntime
}

dependencies {
    compile project(":core")
    compile project(":platformJvm")
    compile project(":languageJvm")
    compile project(":languageScala")
    compile project(":javascript")
    compile project(":diagnostics")

    integTestRuntime project(":compositeBuilds")
    integTestRuntime project(":idePlay")
    testFixturesCompile project(":internalIntegTesting")
    testFixturesCompile libraries.commons_httpclient
}

useTestFixtures()
useTestFixtures(project: ":languageScala", sourceSet: 'integTest')
useTestFixtures(project: ":languageJava", sourceSet: 'integTest')
useTestFixtures(project: ':languageJvm', sourceSet: 'testFixtures')
useTestFixtures(project: ":launcher", sourceSet: 'testFixtures')
useTestFixtures(project: ":dependencyManagement")
useTestFixtures(project: ":diagnostics")
useTestFixtures(project: ':platformBase')

useClassycle()
strictCompile()

task integTestPrepare(type: org.gradle.testing.IntegrationTest) {
    systemProperties['org.gradle.integtest.executer'] = 'embedded'
    if (isCiServer) {
        systemProperties['org.gradle.integtest.multiversion'] = 'all'
    }
    testClassesDirs = sourceSets.integTestPrepare.output.classesDirs
    classpath = sourceSets.integTestPrepare.runtimeClasspath
    maxParallelForks = 1
}

tasks.withType(org.gradle.testing.IntegrationTest) { task ->
    if (task != integTestPrepare) {
        task.dependsOn(integTestPrepare)
    }
}
